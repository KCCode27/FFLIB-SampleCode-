public with sharing class InvoicingService 
{
	public static List<Id> generate(List<Id> sourceRecordIds)
	{
		return service().generate(sourceRecordIds);
	}

	private static IInvoicingService service()
	{
		return (IInvoicingService) Application.Service.newInstance(IInvoicingService.class);
	}	
	
	/**
	 * Interface implemented by domain classes that support the ability to produce invoices
	 **/
	public interface ISupportInvoicing 
	{
		void generate(InvoiceFactory invoiceFactory);
	}
	
	/**
	 * Methods on this factory will help create and register with the UOW invoices
	 */
	public class InvoiceFactory
	{
		private fflib_ISObjectUnitOfWork unitOfWork;

		/**
		 * List of Invoices generated by the factory
		 **/
		public List<Invoice__c> Invoices {get; private set;}
		
		public InvoiceFactory(fflib_ISObjectUnitOfWork unitOfWork)
		{
			this.unitOfWork = unitOfWork;
			this.Invoices = new List<Invoice__c>();
		}
		
		public void add(Invoice invoice)
		{
			Invoice__c invoiceRecord = new Invoice__c();
			invoiceRecord.Account__c = invoice.Account;
			invoiceRecord.Description__c = invoice.Description;
			invoiceRecord.InvoiceDate__c = invoice.InvoiceDate;
			unitOfWork.registerNew(invoiceRecord);
			if(invoice.Lines!=null)
			{
				for(InvoiceLine line : invoice.Lines)
				{
					InvoiceLine__c invoiceLineItem = new InvoiceLine__c();
					invoiceLineItem.Description__c = line.Description;
					invoiceLineItem.Product__c = line.Product;
					invoiceLineItem.UnitPrice__c = line.UnitPrice;
					invoiceLineItem.Quantity__c = line.Quantity;
					unitOfWork.registerNew(invoiceLineItem, InvoiceLine__c.Invoice__c, invoiceRecord);				
				}
			}
			Invoices.add(invoiceRecord);
		}
	}	

	/**
	 * Invoice header
	 **/
	public class Invoice
	{
		public List<InvoiceLine> Lines {get;set;}
		public Id Account {get;set;}
		public String Description {get;set;}
		public Date InvoiceDate {get;set;}
	}

	/**
	 * Invoice line
	 **/
	public class InvoiceLine
	{
		public String Description {get;set;}
		public Id Product {get;set;}
		public Decimal UnitPrice {get;set;}
		public Decimal Quantity {get;set;}
	}
}
